apply plugin:'groovy'
apply plugin:'maven'
apply plugin: 'signing'

group = 'org.grails'
version = "1.0.0.BUILD-SNAPSHOT"
ext {
    isBuildSnapshot = version.endsWith( "BUILD-SNAPSHOT" )
    signedFiles = []
}


repositories {

    mavenCentral()
    maven { url "http://repo.spring.io/libs-milestone" }
    mavenLocal()
}
dependencies {
    compile 'org.springframework.boot:spring-boot-cli:1.0.0.RC1'
    compile "org.springframework.boot:spring-boot-autoconfigure:1.0.0.RC1"
    compile "org.grails:grails-datastore-gorm-hibernate4:3.0.0.BUILD-SNAPSHOT"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testRuntime "com.h2database:h2:1.3.173"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}


signing {
    signedFiles = sign( configurations.archives )
//    required { !isBuildSnapshot  }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.hasProperty("sonatypeUsername") ? project.sonatypeUsername : null,
                        password: project.hasProperty("sonatypePassword") ?  project.sonatypePassword : null)
            }
            snapshotRepository(url: "http://repo.grails.org/grails/libs-snapshots-local") {
                authentication(userName: project.hasProperty("artifactoryPublishUsername") ? project.artifactoryPublishUsername : null,
                        password: project.hasProperty("artifactoryPublishPassword") ?  project.artifactoryPublishPassword : null)
            }

            pom.project {
                name 'Grails GORM for Hibernate 4 integration for Spring Boot'
                packaging 'jar'
                description 'Grails GORM for Hibernate 4 integration for Spring Boot'
                delegate.url 'http://www.github.com/grails/grails-boot/gorm-hibernate4-spring-boot/'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                scm {
                    delegate.url 'scm:git@github.com:grails/grails-boot.git'
                    connection 'scm:git@github.com:grails/grails-boot.git'
                    developerConnection 'scm:git@github.com:grails/grails-boot.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        delegate.url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'graemerocher'
                        name 'Graeme Rocher'
                    }
                }
            }
        }

    }
}


